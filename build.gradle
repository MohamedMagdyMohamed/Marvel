// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "io.gitlab.arturbosch.detekt"

buildscript {
    ext {
        kotlin_version = '1.4.32'
        core_version = '1.5.0'
        appcompat_version = '1.3.0'
        material_version = '1.3.0'
        constraintlayout_version = '2.0.4'
        nav_version = '2.3.5'
        lifecycle_version = '2.3.1'
        hilt_version = '2.35'
        hilt_android_x_version = '1.0.0-alpha02'
        retrofit_version = '2.9.0'
        logging_interceptor_version = '4.7.2'
        pagingVersion = '3.0.0'

        ktlint_version = '10.0.0'
        detekt_version = '1.17.1'
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        // ktlint
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_version"

        // Detekt
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"

        // Hilt
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "io.gitlab.arturbosch.detekt"

    ktlint {
        version = "0.40.0"
        debug = true
        verbose = true
        android = false
        outputToConsole = true
        outputColorName = "RED"
        ignoreFailures = true
        enableExperimentalRules = true
        reporters {
            reporter "plain"
            reporter "checkstyle"
        }
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
        }
    }

    detekt {
        toolVersion = "1.17.1"
        input = files(rootProject.rootDir)
        config = rootProject.files("config/detekt/detekt.yml")
        failFast = false

        reports {
            html.enabled = true // observe findings in your browser with structure and code snippets
            xml.enabled = true // checkstyle like format mainly for integrations like Jenkins
            txt.enabled = true
            // similar to the console output, contains issue signature to manually edit baseline files
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}